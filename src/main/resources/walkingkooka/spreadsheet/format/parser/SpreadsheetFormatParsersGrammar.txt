(* expression .........................................................................*)
EXPRESSION                    = [ EXPRESSION_COMPONENT ],
                                [ EXPRESSION_SEPARATOR, [ EXPRESSION_COMPONENT ],
                                [ EXPRESSION_SEPARATOR, [ EXPRESSION_COMPONENT ],
                                [ EXPRESSION_SEPARATOR, [ (GENERAL | TEXT) ]]
                                ]];

EXPRESSION_COMPONENT          = GENERAL | FRACTION | NUMBER | DATETIME;



(* color .................................................................................*)
COLOR                         = BRACKET_OPEN,
                                ( COLOR_AND_NUMBER | COLOR_NAME ),
                                [ WHITESPACE ],
                                BRACKET_CLOSE;



(* condition .................................................................................*)
CONDITION                     = CONDITION_EQUAL | CONDITION_GREATER_THAN_EQUAL | CONDITION_GREATER_THAN |
                                CONDITION_LESS_THAN_EQUAL | CONDITION_LESS_THAN | CONDITION_NOT_EQUAL;

CONDITION_EQUAL               = BRACKET_OPEN,
                                [ WHITESPACE ],
                                EQUALS,
                                [ WHITESPACE ],
                                CONDITION_NUMBER,
                                [ WHITESPACE ],
                                BRACKET_CLOSE;
                                
CONDITION_GREATER_THAN        = BRACKET_OPEN,
                                [ WHITESPACE ],
                                GREATER_THAN,
                                [ WHITESPACE ],
                                CONDITION_NUMBER,
                                [ WHITESPACE ],
                                BRACKET_CLOSE;

CONDITION_GREATER_THAN_EQUAL  = BRACKET_OPEN,
                                [ WHITESPACE ],
                                GREATER_THAN_EQUALS,
                                [ WHITESPACE ],
                                CONDITION_NUMBER,
                                [ WHITESPACE ],
                                BRACKET_CLOSE;
                                
CONDITION_LESS_THAN           = BRACKET_OPEN,
                                [ WHITESPACE ],
                                LESS_THAN,
                                [ WHITESPACE ],
                                CONDITION_NUMBER,
                                [ WHITESPACE ],
                                BRACKET_CLOSE;

CONDITION_LESS_THAN_EQUAL     = BRACKET_OPEN,
                                [ WHITESPACE ],
                                LESS_THAN_EQUALS,
                                [ WHITESPACE ],
                                CONDITION_NUMBER,
                                [ WHITESPACE ],
                                BRACKET_CLOSE;
                                    
CONDITION_NOT_EQUAL           = BRACKET_OPEN,
                                [ WHITESPACE ],
                                NOT_EQUALS,
                                [ WHITESPACE ],
                                CONDITION_NUMBER,
                                [ WHITESPACE ],
                                BRACKET_CLOSE;



(* date ..............................................................................................................*)
DATE                          = [ CONDITION ], [{ DAY | MONTH_MINUTE | YEAR | DATE_DATETIME_TIME }];



(* date/time .........................................................................................................*)
DATETIME                      = [ CONDITION ], [{ DAY | MONTH_MINUTE | YEAR | HOUR | SECOND_MILLIS | AM_SLASH_PM | A_SLASH_P | DATE_DATETIME_TIME }];



(* time ..............................................................................................................*)
TIME                          = [ CONDITION ], [{ HOUR | MONTH_MINUTE | SECOND_MILLIS | AM_SLASH_PM | A_SLASH_P | DATE_DATETIME_TIME }];



(* date, datetime, time ..............................................................................................*)
DATE_DATETIME_TIME            = COLOR | ESCAPE | DATETIME_TEXT_LITERAL | QUOTED;


(* seconds decimal zeroes.............................................................................................*)
SECOND_MILLIS                 = SECOND,
                                [ DECIMAL_POINT, [{DIGIT_ZERO}]];




(* fractions .........................................................................................................*)
FRACTION                      = FRACTION_COMPONENT,
                                FRACTION_SYMBOL,
                                FRACTION_COMPONENT;

FRACTION_COMPONENT            = [{ FRACTION_OR_LITERAL }],
                                FRACTION_DIGIT,
                                [{ FRACTION_DIGIT | FRACTION_OR_LITERAL }];
FRACTION_OR_LITERAL           = COLOR | CONDITION | CURRENCY | ESCAPE | NUMBER_LITERAL | PERCENTAGE | QUOTED;
FRACTION_DIGIT                = DIGIT | DIGIT_SPACE | DIGIT_ZERO | THOUSANDS;



(* NUMBER .............................................................................................................*)
NUMBER                        = [CONDITION], { COLOR | CURRENCY | DECIMAL_POINT | ESCAPE | NUMBER_EXPONENT | NUMBER_OR_LITERAL | PERCENTAGE | QUOTED };

NUMBER_EXPONENT               = NUMBER_EXPONENT_SYMBOL,
                                [{ DECIMAL_POINT | NUMBER_DIGIT | NUMBER_OR_LITERAL }];
NUMBER_EXPONENT_SYMBOL        = ( 'E+' | 'e+' | 'E-' | 'e-' );

NUMBER_DIGIT                  = DIGIT | DIGIT_SPACE | DIGIT_ZERO | THOUSANDS;

NUMBER_OR_LITERAL             = { COLOR | CURRENCY | DECIMAL_POINT | ESCAPE | NUMBER_DIGIT | NUMBER_LITERAL | QUOTED };




(* general ...........................................................................................................*)
GENERAL                       = [{ GENERAL_COLOR_WHITESPACE }],
                                GENERAL_SYMBOL,
                                [{ GENERAL_COLOR_WHITESPACE }];

GENERAL_COLOR_WHITESPACE      = WHITESPACE | COLOR;



(* text .................................................................................*)
TEXT                          = TEXT_WITH_STAR | TEXT_WITHOUT_STAR;

TEXT_WITH_STAR                = [{ TEXT_COMPONENT }],
                                STAR,
                                [{ TEXT_COMPONENT }];

TEXT_WITHOUT_STAR             = [{ TEXT_COMPONENT }];

TEXT_COMPONENT                = COLOR | DATETIME_TEXT_LITERAL | ESCAPE | QUOTED | TEXT_PLACEHOLDER | UNDERSCORE;


